<INICIAL*> -> <PROG> 'a';
<PROG> -> 'programa' 'id' 'pvirg' <DECLS> <C-COMP>;
<DECLS> -> 'lambda' | 'variaveis' <LISTDECLS> ;

<LISTDECLS> -> <DECL-TIPO> <D>;
<D> -> 'lambda' | <LISTDECLS>;
#<LISTDECLS> -> 'lambda' | <LISTDECLS> <DECL-TIPO>;
<DECL-TIPO> -> <LIST-ID> 'dpontos' <TIPO> 'pvirg';
<LIST-ID> -> 'id' <E>;
<E>	-> 'lambda' | 'virg' <LIST-ID>;
<TIPO> -> 'inteiro' | 'real' | 'logico' | 'caracter';
<C-COMP> -> 'abrech' <LISTA-COMANDOS> 'fechach' ;
<LISTA-COMANDOS> -> <COMANDOS> <G>;
<G> -> 'lambda' | <LISTA-COMANDOS>;
<COMANDOS> -> <IF> | <WHILE> | <READ> | <WRITE> | <ATRIB>;
<IF> -> 'se' 'abrepar' <EXPR> 'fechapar' <C-COMP> <H> ;
<H> -> 'lambda' | 'senao' <C-COMP> ;
<WHILE> -> 'enquanto' 'abrepar' <EXPR> 'fechapar' <C-COMP> ;
<READ> -> 'leia' 'abrepar' <LIST-ID> 'fechapar' 'pvirg';
<ATRIB> -> 'id' 'atrib' <EXPR> 'pvirg';
<WRITE> -> 'escreva' 'abrepar' <LIST-W> 'fechapar' 'pvirg';
<LIST-W> -> <ELEM-W> <L>;
<L> -> 'lambda' | 'virg' <LIST-W>;
<ELEM-W> -> <EXPR> | 'cadeia';
<EXPR> -> <SIMPLES> <P>;
<P> -> 'lambda' | 'oprel' <SIMPLES>;
<SIMPLES> -> <TERMO> <R>;
<R> -> 'lambda' | 'opad' <SIMPLES>;
<TERMO> -> <FAT> <S>;
<S> -> 'lambda' | 'opmul' <TERMO>;
<FAT> -> 'id' | 'cte' | 'abrepar' <EXPR> 'fechapar' | 'verdadeiro' | 'falso' | 'opneg' <FAT>;